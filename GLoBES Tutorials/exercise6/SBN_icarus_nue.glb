%!GLoBES

$version="3.2.13"

nuflux(#ICARUSplus)<			/* NAME can be replaced, e.g. FLUXplus */
	@flux_file ="./flux/ICARUSplus.dat"	/* loads the flux file */
	@time =	3.       		/* sets the runtime in units years */
  	@power = 2.2                   /* e20 POT per year */
        @norm = 1.61e-3		       /* sets a normalization factor */
>

$target_mass = 	0.476        	/* kt (fiducial mass) */


$sampling_points = 99         /* number of sampling points 
				   before energy smearing */
$sampling_min = 0.025             /* GeV */
$sampling_max = 4.975             /* GeV */


$emin = 0.2	        	/* GeV */
$emax = 3.0	        	/* GeV */

$binsize = {0.15 , 0.15 , 0.15 , 0.15 , 0.15 , 0.15 , 0.20 , 0.20 , 0.25 , 0.25, 1.0 } /* bins with different sizes */


$baseline = 0.6   /* km */
$profiletype = 1. /* constant density */


energy(#electron)<	        /* energy resolution for electrons and photons */
    @type = 1
    @sigma_function = #standard
    @sigma_e = {0.0,0.15,0.0} 	
>


$filter_state = 1
$filter_value = 0.0


cross(#CC)<
	@cross_file = "./xsec/XCC.dat"
>

cross(#NC)<
	@cross_file = "./xsec/XNC.dat"
>


channel(#nu_e_signal)<
	@channel = 	#ICARUSplus:	+:	m:	e:	#CC:    #electron
>

channel(#anu_e_signal)<
	@channel = 	#ICARUSplus:	-:	m:	e:	#CC:    #electron
>


rule(#Nu_E_Appearance)<
 	@signal = 0.0163@#nu_e_signal : 0.0163@#anu_e_signal	
	@background = 0.0@#nu_e_signal : 0.0@#anu_e_signal
        @energy_window = 0.2 : 3.0	
        
        @sys_off_function = "chiNoSysSpectrum"
        @sys_off_errors   = {}
        @sys_on_function  = "chiSBN"
        @sys_on_errors    = { 0.1, 0.1}
                         /* { SigNorm, BGNorm} */
>
